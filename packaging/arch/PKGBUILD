# Maintainer: YangYuS8 <https://github.com/YangYuS8>

pkgname=pnet-tool
pkgver=0.2.1
pkgrel=2
pkgdesc="PNETLab 定制的 Telnet 客户端（Tauri deb 发行产物复用）"
arch=('x86_64')
url="https://github.com/YangYuS8/pnet-tool"
license=('custom')
depends=('xdg-utils' 'desktop-file-utils' 'gtk3')
makedepends=('curl' 'axel' 'libarchive')
DLAGENTS=('https::/usr/bin/axel -n 8 -o %o %u'
          'http::/usr/bin/axel -n 8 -o %o %u')
options=('!strip')
source=(
  "pnet-tool_${pkgver}_amd64.deb::https://github.com/YangYuS8/pnet-tool/releases/download/v${pkgver}/pnet-tool_${pkgver}_amd64.deb"
  "pnet-tool.desktop"
  "pnet-tool.svg"
)
sha256sums=('SKIP' 'SKIP' 'SKIP')
install=pnet-tool.install

package() {
  # extract deb
  mkdir -p "$srcdir/deb-extract"
  bsdtar -xvf "$srcdir/pnet-tool_${pkgver}_amd64.deb" -C "$srcdir/deb-extract"
  local data_tar
  data_tar=$(ls "$srcdir/deb-extract"/data.tar.* 2>/dev/null | head -n1)
  if [[ -z "$data_tar" ]]; then
    echo "data.tar.* not found under $srcdir/deb-extract" >&2
    return 1
  fi
  bsdtar -xvf "$data_tar" -C "$srcdir/deb-extract"

  # install files from deb layout
  cp -dr --no-preserve=ownership "$srcdir/deb-extract/usr" "$pkgdir/"
  cp -dr --no-preserve=ownership "$srcdir/deb-extract/opt" "$pkgdir/" 2>/dev/null || true

  # ensure /usr/bin/pnet-tool exists; if missing, create symlink to /opt
  if [[ ! -e "$pkgdir/usr/bin/pnet-tool" ]]; then
    install -d "$pkgdir/usr/bin"
    ln -s /opt/pnet-tool/pnet-tool "$pkgdir/usr/bin/pnet-tool"
  fi

  # ensure desktop and icon exist (override with repo versions if provided)
  install -Dm644 "$srcdir/pnet-tool.desktop" "$pkgdir/usr/share/applications/pnet-tool.desktop"
  install -Dm644 "$srcdir/pnet-tool.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/pnet-tool.svg"
}
